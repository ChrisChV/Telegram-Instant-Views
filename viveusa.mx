~version: "2.0"


@if(//div[has-class("field-name-body")]){
  $tbody: //div[has-class("field-name-body")]
}
@if_not(//div[has-class("field-name-body")]) {
  @if(//div[has-class("views-field-body")]){
    $tbody: //div[has-class("views-field-body")]
    @prepend_to($tbody): //div[has-class("views-field-field-embed")]
    @replace_tag(<related>): $tbody//ul
  }
  @if_not(//div[has-class("views-field-body")]) {
    $tbody: //header
    @wrap_inner(<div>): $tbody
    @remove: $tbody/div
  }
}

body: $tbody




title: //h1[has-class("block-title")]
author: //meta[@property="autor"]/@content
author: //div[has-class("field-name-field-autor")]
subtitle: //div[has-class("field-name-field-resumen")]
subtitle: //div[has-class("views-field-field-resumen")]
description: $subtitle

@replace_tag(<figure>): //div[has-class("field-name-field-image")]
@append(<figcaption>): $@
@append_to(//figure[has-class("field-name-field-image")]/figcaption): //div[has-class("field-name-field-sumario-imagen")]
@append_to(//figure[has-class("field-name-field-image")]/figcaption): //div[has-class("field-name-field-credito-imagen")]
@replace_tag(<cite>): $@

cover: //figure[has-class("field-name-field-image")]

@replace("T", " "): //meta[@name="fecha_publicacion"]/@content
@replace(":[0-9][0-9]-.*", "")
published_date: $@

@replace_tag(<div>): $tbody//p/iframe/..
@replace_tag(<div>): $tbody//a/img/..
@replace_tag(<div>): $tbody//p/img/..

@replace_tag(<figure>): $tbody//div[@class="rtecenter"]
@append_to($@): $@/next-sibling::p[@class="rtecenter"]
@replace_tag(<figcaption>): $@



@replace_tag(<related>): $tbody//div[has-class("promo-embed")]
@map($@){
  $trelated: $@
  @replace_tag(<h1>): $trelated/prev-sibling::p/span/span[@class="node-embed-title"]
  @append_to($trelated): $@
}



@prepend_to($tbody): //div[has-class("views_slideshow_main")]
$tslide: $@
@if($tbody//p) {
  @replace_tag(<slideshow>): $tslide
  $tslide: $@
}
@replace_tag(<figure>): $tslide/div/div
@replace_tag(<figcaption>): $@//div[@class="galeria_piefoto"]
@remove: $@/div


@remove: $tbody//related//div[@class="imagen"]
@remove: $tbody//related//div[@class="resumen"]
@remove: $tbody//a[text()="via GIPHY"]
#@remove: $tbody//a/strong/span[@style="color:#FF0000;"]/../..

@remove: $tbody//iframe[@class="mobRev"]




